 %import common.CNAME    -> IDENT
 %import common.HEXDIGIT -> HEXDIGIT
 %import common.DIGIT    -> DIGIT
 %import common.SH_COMMENT

start: stmt*

?stmt: label_def
    | directive_block
    | macro_call
    | instruction
    | _NEWLINE

label_def: IDENT ":"

?directive_block: data_block | macro_block

data_block: ".data" (label_def | value | _NEWLINE)* ".endd"
macro_block: ".macro" IDENT  macro_args _NEWLINE stmt+ ".endm"
macro_args: IDENT*

macro_call: IDENT "!" macro_args ";"

instruction: IDENT args? _NEWLINE
args: value ("," value)*

?value: IDENT | NUMBER | QMARK
NUMBER: "-"? DEC_INT | HEX_INT | BIN_INT

DEC_INT: DIGIT+
HEX_INT: "0x" HEXDIGIT+
BIN_INT: "0b" BINDIGIT+

BINDIGIT: "0" | "1"

QMARK: "?"

_NEWLINE: "\n"


%ignore /[ \t]+/
%ignore SH_COMMENT