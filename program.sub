.macro subleq! a b c;
    a b c;
.endm

.macro jmp! a;
    z z a;
.endm

.macro clr! a;
    a;
.endm

######## b = b + a ########
.macro add! a b;
    a z; z b; z;
.endm

.macro sub! a b;
    a b;
.endm

######## b = a ########
.macro cpy! a b;
    b; a z; z b; z;
.endm


######## a = a - 1 ########
.macro dec! a;
    p1 a;
.endm

######## a = a + 1 ########
.macro inc! a;
    m1 a;
.endm

######## if a <= 0: jmp b ########
.macro jleqz! a b;
    z a b;
.endm


######## if a > 0: jmp b ########
.macro jgtz! a b;
                            # not(a <= 0) -> jump
    jleqz! a return;        # a <= 0, don't take the jump to b
    jmp! b;
    return:
.endm

######## if a == 0: jmp b ########
.macro jeqz! a b;
                            # not(a > 0) and (a+1 > 0) -> jump
    jgtz! a return;         # a > 0, do not jump
    inc! a;
    jgtz! a decjump;        # a+1 > 0, jump
    dec! a;                 # fall through to de-increment a
    jmp! return;
    decjump:
        dec! a;
        jmp! b;
    return:
.endm

######## if a >= 0: jmp b ########
.macro jgeqz! a b;
                            # (a > 0) or (a == 0) -> jump
    jgtz! a b;              # a > 0, jump
    jeqz! a b;              # a == 0, jump
.endm

######## if a <  0: jmp b ########
.macro jltz! a b;
                            # not(a == 0) and (a <= 0) -> jump
    jeqz! a return;         # a == 0: therefor not a < 0, return
    jleqz! a b;             # a <= 0, but not 0 -> a < 0, take the jump
    return:
.endm



######## b = b * a ########
.macro mul! a b;
        inc! a;
    loop:
        p1 a break; # decrement 'a' by 1, break if 0
        add! b tmp;
        z z loop;
    break:
        b; add! tmp b; tmp tmp return;

    .data tmp: 0 .endd
    return:
.endm

######## a = a + a ########
.macro double! a;
    add! a a;
.endm

######## b = b << a ########
.macro lshift! a b;
    cpy! a counter;
    inc! counter;
    loop:
        p1 counter return;                                        # decrement counter by 1, return if 0
        double! b; jmp! loop;
    .data counter: 0 .endd
    return:
.endm


######## b = b >> a ########
.macro rshift! a b;
        
        add! literal_16 count;
        b count; 
        inc! count;

        p1 count end; # if count is <= 1: end

        z z rshift_start;

    shift:
        double! a; double! out;

    rshift_start:
        tmp tmp; add! a tmp; m1 tmp inc_out;         # if the first bit of a is 1, inc out else shift
        tmp tmp check_break;

    inc_out:
        m1 out;

    check_break:
        p1 count end; # if count is <= 1: end
        z z shift;

    end:
        b b; add! out b; z z return;

    .data 
        count: 0
        tmp: 0
        out: 0
    .endd

    return:
.endm

####### b = b * a #########
.macro fastmul! a b; # WIP
    while:
        jleqz! b return;                                           # if b <= 0: return
        cpy! b tmp; lshift! literal_15 tmp; jgeqz! tmp shift;      # if b & 1:
        add! a result; result IO;                                  #     result += a
    shift:
        double! a;                                                 # double a
        rshift! p1 b;                                              # halve b
        jmp! while;
    .data result: 0 tmp: 0 .endd
    return:
        cpy! result b;
.endm





# dest is the place where we want the value
# ptr is the addrs of the -value (as long as we negate the number going in, we can negate it going out)
# executes the date block, falls through
.macro read_deref! ptr dest;
    # b <- a
    
    clr! code_a;
    clr! code_a0;
    clr! code_a1;
    ptr z;
    z code_a;
    z code_a0;
    z code_a1; 
    clr! z;
    cpy! ptr code_a;
    dest dest code_a;             # clear dest and jump code_a

    .data
        # Two self modifying instructions
        code_a: 0                # dest -= *code_a
                dest
                ?
       code_a0: 0                # clr! *code_a
       code_a1: 0
                ?
    .endd
.endm

.macro write_deref! src ptr;
    # b <- a

    cpy! ptr code_b;
    jmp! code_a;

    .data
        code_a:  src              # This will become: subleq val, dest, ...
        code_b:  0
        code_c:  ?                # next instruction
    .endd
.endm

.macro push! a;
    write_deref! a stack_ptr;
    inc! stack_ptr;
.endm

.macro pop! a;
    dec! stack_ptr;
    read_deref! stack_ptr a;
.endm

.macro call_subroutine! func_addr;
    write_deref! return_addr stack_ptr; inc! stack_ptr;
    jmp! func_addr;
    .data return_addr: ? .endd
    return: 
.endm

.macro subroutine_boilerplate!;
        jleqz! second_pass setup;     # if second_pass <= 0 then jmp to setup 
        clr! second_pass;             #     else clean up second_pass and ...
        .data
            0                         # self modifying jmp to return_addr
            0
            return_addr: 0

            second_pass: 0            # flag to branch on rerun
        .endd
    setup:
        inc! second_pass;
        pop! return_addr;            # pop the return addr from the stack and put into self modifying jmp
.endm

######## 4 bit shift; inc output if overflow ########
.macro quarter_word_lshift_overflow! input output;
                            # 0000_0000_0000_1000
        double! input;      # 0000_0000_0001_0000
        cpy! input tmp;
        double! tmp;        # 0000_0000_0010_0000
        double! tmp;        # 0000_0000_0100_0000
        double! tmp;        # 0000_0000_1000_0000

        double! tmp;        # 0000_0001_0000_0000
        double! tmp;        # 0000_0010_0000_0000
        double! tmp;        # 0000_0100_0000_0000
        double! tmp;        # 0000_1000_0000_0000

        double! tmp;        # 0001_0000_0000_0000
        double! tmp;        # 0010_0000_0000_0000
        double! tmp;        # 0100_0000_0000_0000
        double! tmp;        # 1000_0000_0000_0000
        
        jgeqz! tmp no_overflow;
        inc! output;
        sub! literal_16 input;
    no_overflow:
.endm

######## 8 bit shift; inc output if overflow ########
.macro half_word_lshift_overflow! input output;
        double! input;
        cpy! input tmp;
        double! tmp;
        double! tmp;
        double! tmp;
        double! tmp;

        double! tmp;
        double! tmp;
        double! tmp;
        jgeqz! tmp return;
        inc! output;
        sub! literal_256 input;
    return:
.endm

######## 16 bit shift; inc output if overflow ########
.macro lshift_overflow! input output;
        jgeqz! input no_overflow;
        inc! output;                 # inc output if overflow, always shift input
    no_overflow:
        double! input;
.endm


.macro newline!;
    ascii_lf IO;
    ascii_cr IO;
.endm

.macro double_dabble_add_3! x;
    cpy! x tmp;
    subleq! literal_4 tmp return;    # if x â‰¤ 4, skip
    add! literal_3 x;                # else x += 3
    jmp! return;
    .data tmp: 0 .endd
return:
.endm

################################################
#################### CODE ######################
################################################
jmp! test;
# ALIGNMENT
.data 
    0     # IO
    0     # INSPECT
.endd                    



func_print_bin:
    subroutine_boilerplate!;
    pop! func_print_bin_a;

    cpy! literal_16 func_print_bin_counter;

    func_print_bin_check_msb:
        jltz! func_print_bin_a func_print_bin_print_1;

    func_print_bin_print_0:
        ascii_0 IO;
        jmp! func_print_bin_shift;

    func_print_bin_print_1:
        ascii_1 IO;
    
    func_print_bin_shift:
        p1 func_print_bin_counter func_print_bin_return;
        double! func_print_bin_a;
        jmp! func_print_bin_check_msb;
    
    func_print_bin_return:
        ascii_lf IO;
        ascii_cr IO;
        jmp! func_print_bin;

    .data
        func_print_bin_a: 0
        func_print_bin_counter: 0
    .endd


    
test:    
    # clr! input;
    # IO input;

    clr! ones;
    clr! tens;
    clr! hund;
    clr! thou;
    clr! tthou;

    
    # tthou INSPECT;
    # thou INSPECT;
    # hund INSPECT;
    # tens INSPECT;
    # ones INSPECT;
    # input INSPECT;
    # newline!;



shift:
    double_dabble_add_3! thou;
    double_dabble_add_3! hund;
    double_dabble_add_3! tens;
    double_dabble_add_3! ones;

    double! tthou;
    quarter_word_lshift_overflow! thou tthou;
    quarter_word_lshift_overflow! hund thou;
    quarter_word_lshift_overflow! tens hund;
    quarter_word_lshift_overflow! ones tens;
    lshift_overflow! input ones;


    cpy! ascii_0 a;
    add! tthou a;
    a IO;
    cpy! ascii_0 a;
    add! thou a;
    a IO;
    cpy! ascii_0 a;
    add! hund a;
    a IO;
    cpy! ascii_0 a;
    add! tens a;
    a IO;
    cpy! ascii_0 a;
    add! ones a;
    a IO;
    newline!;

    # EXPECT 0x750a -> 29962
    
    subleq! p1 counter print;
    jmp! shift;
print:
    jmp! HALT;

.data
    counter: 16

    ones: 0
    tens: 0
    hund: 0
    thou: 0
    tthou: 0

    a: 0
    b: 0
    c: 0
    d: 0
    input: 0x750a
    stack: 0 0 0 0 0 0 0 0 0 0
    stack_ptr: stack
    dest: 0

    z: 0
    p1: 1
    m1: -1

    tmp: 0

    literal_2: 2
    literal_3: 3
    literal_4: 4
    literal_5: 5
    literal_16: 16
    literal_15: 15
    literal_256: 256

    ascii_lf: 10
    ascii_cr: 13
    ascii_0: 48
    ascii_1: 49
.endd
